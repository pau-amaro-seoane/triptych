dynamics 	#plot orbital trajectories

	era

	expand 1.3
	data dynamics.dat

	lines 2 99999
	read{x1 7 y1 8 x2 9 y2 10}

	set x =x1 concat x2
	set y =y1 concat y2
	set both = x concat y

	limits both both
	box
	
	ptype 8 3
	set xi = $(x1[0])
	set yi = $(y1[0])
	points xi yi
	ptype 8 0
	set xi = $(x2[0])
	set yi = $(y2[0])
	points xi yi

	ltype 0
	connect x1 y1
	ltype 1
	connect x2 y2
	xlabel x [R_\odot]
	ylabel y [R_\odot]
	ltype 0
	relocate (11000 31555)
	label Initial Orbital Trajectories

product	#plot structure and composition profiles

	era
	expand 1.3
	data product.dat
	lines 2 9999
	read {m 1 pres 2 den 3 r 4 hyd 5 Z 6}
	
	window 1 -4 1 1
	LOCATION $($gx1 + 2000) $gx2 $gy1 $gy2	
	limits m den
	box
	connect m den
	xlabel m [M_\odot]
	ylabel \rho \ [g/cm^3]

	window 1 -4 1 2
	LOCATION $($gx1 + 2000) $gx2 $gy1 $gy2
	limits m pres
	box 0 2
	connect m pres
	ylabel P [dyne/cm^2]

	window 1 -4 1 3
	LOCATION $($gx1 + 2000) $gx2 $gy1 $gy2	
	limits m r
	box 0 2
	connect m r
	ylabel r [R_\odot]

	window 1 -4 1 4
	LOCATION $($gx1 + 2000) $gx2 $gy1 $gy2	
	limits m hyd
	box 0 2
	connect m hyd
	ylabel X
	relocate (4500 31555)
	label Collision Product Composition and Structure Profiles
evolution	#plot evolution data

	era
	expand 1.3
	data evolution.dat
	lines 2 9999
	read {age 1 M 2 R 3 Y 4 Z 5}
	
	window 1 -4 1 1
	limits age M
	box
	connect age M
	xlabel t [Myr]
	ylabel M [M_\odot]

	window 1 -4 1 2
	limits age R
	box 0 2
	connect age R
	ylabel R [R_\odot]

	window 1 -4 1 3
	limits age Y
	box 0 2
	connect age Y
	ylabel Y

	window 1 -4 1 4
	limits age Z
	box 0 2
	connect age Z
	ylabel Z
	relocate (11100 31555)
	label Collision Product Evolution
